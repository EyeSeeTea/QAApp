buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
        mavenLocal()
        google()
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

def cfg = rootProject.ext.configuration
def libs = rootProject.ext.libraries

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    testOptions {
        unitTests.all {
            // All the usual Gradle options.
            testLogging {
                events "skipped", "failed", "standardOut", "standardError"
                exceptionFormat "full"
                showCauses true
                showExceptions true
                showStackTraces true
                showStandardStreams true
            }
        }
    }

    signingConfigs {
        config {}
    }
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDirs sharedTestDir
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion cfg.compileSdkVersion
    buildToolsVersion cfg.buildToolsVersion

    defaultConfig {
        applicationId 'org.eyeseetea.malariacare'
        minSdkVersion cfg.minSdkVersion
        targetSdkVersion cfg.targetSdkVersion
        versionCode cfg.versionCode
        versionName cfg.versionName
        multiDexEnabled true
        dexOptions {
            javaMaxHeapSize "6g"
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.config
        useLibrary  'org.apache.http.legacy'
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['targetModuleName': 'EyeSeeTea']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
    //TODO This config is related to a known issue: http://stackoverflow.com/questions/20827885/android-studio-0-4-duplicate-files-copied-in-apk-meta-inf-license-txt
    packagingOptions {
        exclude 'LICENSE.txt'
        //FIXME: This has been automatically suggested by AndroidStudio while sdk integration
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    flavorDimensions "default"

    productFlavors {
        eds {
            applicationId "org.eyeseetea.malariacare.eds_dev"
            versionName "2.0.5"
            buildConfigField "boolean", "translations", "false"
            buildConfigField "String", "defaultLocale", "\"en\""
            resValue "string", "authority", applicationId+versionName+"_DHIS2_DEFAULT_AUTHORITY"
            buildConfigField "boolean", "customFontHidden", "false"
            buildConfigField "boolean", "OUSelectionHealthFacility", "false"
            buildConfigField "String", "GIT_HUB_BOT_TOKEN", "\"$gitHubBotToken\""
        }
        hnqis {
            applicationId "org.eyeseetea.malariacare.hnqis_ng_dev"
            versionName "1.6.5"
            buildConfigField "boolean", "translations", "true"
            buildConfigField "String", "defaultLocale", "\"en\""
            resValue "string", "authority", applicationId+versionName+"_DHIS2_DEFAULT_AUTHORITY"
            buildConfigField "boolean", "customFontHidden", "true"
            buildConfigField "boolean", "OUSelectionHealthFacility", "true"
            buildConfigField "String", "GIT_HUB_BOT_TOKEN", "\"$gitHubBotToken\""
        }
        eyeseetea {
            applicationId "org.eyeseetea.malariacare.questmark_dev"
            versionName "0.1.0"
            buildConfigField "boolean", "translations", "true"
            buildConfigField "String", "defaultLocale", "\"en\""
            resValue "string", "authority", applicationId+versionName+"_DHIS2_DEFAULT_AUTHORITY"
            buildConfigField "boolean", "customFontHidden", "false"
            buildConfigField "boolean", "OUSelectionHealthFacility", "false"
            buildConfigField "String", "GIT_HUB_BOT_TOKEN", "\"$gitHubBotToken\""
        }
    }


    lintOptions {
        disable 'RtlSymmetry', 'RtlHardcoded'
        checkReleaseBuilds false
    }
    configurations.all {
        resolutionStrategy.force "com.google.code.findbugs:jsr305:${libs.jsr305Version}"
    }
}

configurations {
    all*.exclude group: 'com.google.guava', module:'guava-jdk5'
}

dependencies {
    // SDK submodules
    implementation project(':core')
    implementation project(':models')
    implementation project(':ui-bindings')
    implementation project(':EyeSeeTea-sdk')

    // App dependencies
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    apply plugin: 'com.github.dcendents.android-maven'

    //Android support
    implementation "com.android.support:support-v4:${libs.supportVersion}"
    implementation "com.android.support:appcompat-v7:${libs.supportVersion}"
    implementation "com.android.support:support-annotations:${libs.supportVersion}"
    implementation "com.android.support:multidex:${libs.multidex}"
    //3rd party dependencies
    implementation "com.opencsv:opencsv:${libs.opencsvVersion}"
    implementation "com.google.android.gms:play-services:${libs.gmsPlayVersion}"
    implementation "com.google.guava:guava:${libs.guavaVersion}"
    implementation "com.squareup.okhttp3:okhttp:${libs.okhttpVersion}"
    implementation "com.googlecode.libphonenumber:libphonenumber:${libs.libphonenumberVersion}"
    implementation "com.github.PhilJay:MPAndroidChart:v${libs.MPAndroidChartVersion}"

    //Drive dependencies
    implementation "com.google.android.gms:play-services-identity:${libs.gmsPlayVersion}"
    implementation("com.google.api-client:google-api-client-android:${libs.googleApiClientAndroidVersion}") {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.google.code.findbugs'
    }
    implementation("com.google.apis:google-api-services-drive:${libs.googleApiServicesDriveVersion}") {
        exclude group: 'org.apache.httpcomponents'
    }

    implementation "com.google.code.findbugs:jsr305:${libs.jsr305Version}"


    // Square
    implementation "com.squareup.okhttp3:okhttp:${libs.okhttpVersion}"
    implementation "com.squareup.retrofit2:retrofit:${libs.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${libs.retrofitVersion}"

    // Unit testing dependencies
    testImplementation "junit:junit:${libs.jUnitVersion}"
    testImplementation "org.mockito:mockito-core:${libs.mockitoVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${libs.mockwebserverVersion}"
    testImplementation "com.linkedin.dexmaker:dexmaker-mockito:${libs.dexmakerMockitoVersion}"
    testImplementation "com.google.code.gson:gson:${libs.gsonVersion}"
    testImplementation "com.fasterxml.jackson.core:jackson-core:${libs.jacksonVersion}"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:${libs.jacksonVersion}"
    testImplementation "com.fasterxml.jackson.core:jackson-annotations:${libs.jacksonVersion}"
    testImplementation "com.fasterxml.jackson.datatype:jackson-datatype-joda:${libs.jacksonVersion}"
    testImplementation "joda-time:joda-time:${libs.jodaTimeVersion}"

    // Instrumented tests dependencies
    // Android Testing Support Library's runner and rules

    androidTestImplementation "com.android.support.test:runner:${libs.runnerVersion}"
    androidTestImplementation "com.android.support.test:rules:${libs.runnerVersion}"

    // Espresso UI Testing dependencies.
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${libs.espressoVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:${libs.espressoVersion}"
    // Espresso-idling-resource for synchronization with background jobs
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:${libs.espressoVersion}"
    // Espresso-web for WebView support
    androidTestImplementation "com.android.support.test.espresso:espresso-web:${libs.espressoVersion}"

    //Tracking crashes
    implementation("com.crashlytics.sdk.android:crashlytics:${libs.crashlyticsVersion}") {
        transitive = true
    }

    // JavaPoet (sucessor to JavaWriter)
    implementation "com.squareup:javapoet:${libs.javapoetVersion}"

    //DBFlow
    annotationProcessor "com.github.agrosner.dbflow:dbflow-processor:${libs.dbFlowVersion}"
    implementation "com.github.agrosner.dbflow:dbflow:${libs.dbFlowVersion}"
    implementation "com.github.agrosner.dbflow:dbflow-core:${libs.dbFlowVersion}"
    // Instrumented tests dependencies
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${libs.mockwebserverVersion}"
    androidTestImplementation "org.mockito:mockito-core:${libs.mockitoVersion}"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:${libs.dexmakerMockitoVersion}"

    implementation(project(path: ':bugshaker')) {
        exclude group: "com.google.android.gms"
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

check.dependsOn ktlint


